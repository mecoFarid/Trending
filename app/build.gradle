import io.gitlab.arturbosch.detekt.Detekt

plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'kotlin-kapt'

    // Code Style
    id "io.gitlab.arturbosch.detekt" version "$detekt"
    id 'org.jetbrains.kotlin.plugin.serialization' version '1.8.0'
}

allprojects {
    apply plugin: "io.gitlab.arturbosch.detekt" // Version should be inherited from parent

    detekt {
        config = files("$rootDir/detekt_config.yml")
        // point to your custom config defining rules to run, overwriting default behavior
    }
}

android {
    namespace 'com.mecofarid.trending'
    compileSdk rootProject.compileSdk

    defaultConfig {
        applicationId "com.mecofarid.trending"
        minSdk rootProject.minSdk
        targetSdk rootProject.targetSdk
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        kapt {
            arguments {
                arg("room.schemaLocation", "$projectDir/schemas")
            }
        }
    }

    buildFeatures {
        dataBinding = true
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
}

tasks.withType(Detekt).configureEach {
    reports {
        html.enabled = false // observe findings in your browser with structure and code snippets
        xml.enabled = false // checkstyle like format mainly for integrations like Jenkins
        txt.enabled = false // similar to the console output, contains issue signature to manually edit baseline files
        sarif.enabled = false // standardized SARIF format (https://sarifweb.azurewebsites.net/) to support integrations with Github Code Scanning
    }
}

dependencies {

    // Kotlin
    implementation "androidx.core:core-ktx:$kotlin"

    // Kotlin Coroutines
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$kotlinCoroutines"

    // Android UI
    implementation "androidx.appcompat:appcompat:$appCompat"
    implementation "com.google.android.material:material:$materialUi"
    implementation "androidx.constraintlayout:constraintlayout:$constraintLayout"
    implementation "androidx.recyclerview:recyclerview:$recyclerView"
    implementation "androidx.swiperefreshlayout:swiperefreshlayout:$swiperefresh"

    // ViewModel
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$viewModelScope"
    implementation "androidx.activity:activity-ktx:$viewModelActivtyKtx"

    // Room
    implementation "androidx.room:room-runtime:$room"
    implementation "androidx.room:room-ktx:$room"
    annotationProcessor "androidx.room:room-compiler:$room"
    kapt "androidx.room:room-compiler:$room"

    // Retrofit
    implementation "com.squareup.retrofit2:retrofit:$retrofit"
    implementation "com.squareup.retrofit2:converter-moshi:$retrofit"
    implementation "com.squareup.okhttp3:logging-interceptor:$retrofitLogging"
    implementation "com.jakewharton.retrofit:retrofit2-kotlinx-serialization-converter:$retrofitKotlinxSerialization"

    // KotlinX Serialization
    implementation "org.jetbrains.kotlinx:kotlinx-serialization-json:$kotlinxSerialization"

    // Lottie
    implementation "com.airbnb.android:lottie:$lottie"

    // Shimmer
    implementation "com.facebook.shimmer:shimmer:$shimmer"

    // Glide
    implementation "com.github.bumptech.glide:glide:$glide"

    // Test
    testImplementation "junit:junit:$jUnit"
    testImplementation "io.mockk:mockk:$mockk"
    testImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:$kotlinCoroutines"
    testImplementation "androidx.test:core-ktx:$kotlinAndroidTest"
    testImplementation "androidx.test.ext:junit:$jUnitAndroidX"
    testImplementation project(':shared-test')
    testImplementation "com.squareup.okhttp3:mockwebserver:$okHttp"
    testImplementation "androidx.arch.core:core-testing:$androidArchCore"
    androidTestImplementation project(':shared-test')
    androidTestImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:$kotlinCoroutines"
    androidTestImplementation "androidx.test.ext:junit:$jUnitAndroidX"
    androidTestImplementation "androidx.test.espresso:espresso-core:$espresso"
}